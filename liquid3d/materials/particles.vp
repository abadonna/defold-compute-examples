#version 140

in mediump float idx;
in mediump vec2 texcoord0;

//results of compute shaders
uniform sampler2D tex_position;

out mediump vec2 var_texcoord0;
out mediump vec3 var_view_center;
out mediump float var_radius; 

uniform vs_uniforms
{
    uniform mediump mat4 mtx_worldview;
    uniform mediump mat4 mtx_view;
    uniform mediump mat4 mtx_proj;
    uniform mediump vec4 options; // x- size of data texure, y - particle size
};

void main()
{
    float data_width = options.x;
    float particle_size = options.y;
    
    float u = mod(idx, data_width) / data_width;
    float v = floor(idx / data_width) / data_width;

    vec3 pos = texture(tex_position, vec2(u, v)).xyz;
    
    vec4 view_pos = mtx_worldview * vec4(pos, 1.0);
    var_view_center = view_pos.xyz;
    var_radius = particle_size / 2;
    
    vec2 offset = (texcoord0 - 0.5) * particle_size;
    view_pos.xy += offset;

    gl_Position = mtx_proj * view_pos;

    var_texcoord0 = texcoord0;
}

